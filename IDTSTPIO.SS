; /// PIO (I)DE/(S)CSI (I)DENTIFY (T)EST (ISIT) - KOSS 2001-2002 - TAB : 32 ///

; Author   : Kochise
; Date     : 2002/06/22/07
; Type     : TOS program
; Language : Assembler (Motorola 68030)
; Sequence : BOOT/GEM
; Purpose  : ATARI Falcon030 IDE/SCSI devices scanner

; Machina  : ATARI Falcon030 (68030 & TOS based computer/emulator with Falcon030 like IDE & SCSI mapped ports)
; O.S.     : 68030 compatible TOS (ATARI Falcon030 features TOS 4.00+)

	OPT	p=68030/68882
	OPT	AUTOPC
	OPT	BDW
	OPT	O+
	OPT	OW-
	COMMENT	HEAD=7	; FastLoad
;	OUTPUT	\IDTSTPIO.PRG

BS:	EQU	1	; Bytes size
WS:	EQU	2
LS:	EQU	4

BB:	EQU	BS*8	; Bits size
WB:	EQU	WS*8
LB:	EQU	LS*8

FALSE:	EQU	0
TRUE:	EQU	1

; --- EQUATE ASSEMBLY (EA) ----------------------------------------------------

	; IDE

ISIT_EA_IDE_ACTIVE:	EQU	TRUE

	; SCSI

ISIT_EA_SCSI_ACTIVE:	EQU	FALSE

; --- EQUATE DATA (ED) --------------------------------------------------------

ISIT_ED_MAJOR_VERSION:	EQU	$01
ISIT_ED_MINOR_VERSION:	EQU	$02
ISIT_ED_ASSEMBLY_VERSION:	EQU	(ISIT_ED_MAJOR_VERSION<<8)|ISIT_ED_MINOR_VERSION

ISIT_ED_DAY:	EQU	22	; Binary Date Format
ISIT_ED_MONTH:	EQU	06	;  FEDC BA98 7654 3210
ISIT_ED_YEAR:	EQU	2002	; %YYYY YYYM MMMD DDDD
ISIT_ED_ASSEMBLY_DATE:	EQU	((ISIT_ED_YEAR-1980)<<9)|(ISIT_ED_MONTH<<5)|(ISIT_ED_DAY)

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_ED_IDE_CMD_ID_DEV:	EQU	$EC	; IDE COMMAND : IDENTIFY DEVICE
ISIT_ED_IDE_CMD_ID_PDEV:	EQU	$A1	; IDE COMMAND : IDENTIFY PACKET DEVICE

ISIT_ED_IDE_CLK_WAIT:	EQU	1	; 1 second

ISIT_ED_IDE_NUMPORT:	EQU	2	; IDE 0/1

	ENDC ; ---------------------------------------'

	; SCSI
	
	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_ED_SCSI_CLK_WAIT:	EQU	2	; 2 seconds

ISIT_ED_SCSI_NUMPORT:	EQU	8	; SCSI 0-7

	ENDC ; ---------------------------------------'

; --- EQUATE INDEX (EI) -------------------------------------------------------

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_EI_IDE_REG_RO_ASR:	EQU	8*LS
ISIT_EI_IDE_REG_WO_CR:	EQU	7*LS
ISIT_EI_IDE_REG_RW_CHR:	EQU	5*LS
ISIT_EI_IDE_REG_RW_CLR:	EQU	4*LS
ISIT_EI_IDE_REG_RW_DP:	EQU	0*LS
ISIT_EI_IDE_REG_RW_DR:	EQU	0*LS
ISIT_EI_IDE_REG_WO_DCR:	EQU	8*LS
ISIT_EI_IDE_REG_RW_DHR:	EQU	6*LS
ISIT_EI_IDE_REG_RO_ER:	EQU	1*LS
ISIT_EI_IDE_REG_WO_FR:	EQU	1*LS
ISIT_EI_IDE_REG_RW_SCR:	EQU	2*LS
ISIT_EI_IDE_REG_RW_SNR:	EQU	3*LS
ISIT_EI_IDE_REG_RO_SR:	EQU	7*LS
ISIT_EI_IDE_REG_RW_LBA0700:	EQU	3*LS
ISIT_EI_IDE_REG_RW_LBA1508:	EQU	4*LS
ISIT_EI_IDE_REG_RW_LBA2316:	EQU	5*LS
ISIT_EI_IDE_REG_RW_LBA2724:	EQU	6*LS

ISIT_EI_IDE_BIT_DCR_WO_SRST:	EQU	2	; DCR
ISIT_EI_IDE_BIT_DCR_WO_nIEN:	EQU	1	; DCR
ISIT_EI_IDE_BIT_DHR_RW_DEV:	EQU	4	; DHR
ISIT_EI_IDE_BIT_ER_RO_ABRT:	EQU	2	; ER
ISIT_EI_IDE_BIT_SR_RO_BSY:	EQU	7	; SR
ISIT_EI_IDE_BIT_SR_RO_DRDY:	EQU	6	; SR
ISIT_EI_IDE_BIT_SR_RO_DRQ:	EQU	3	; SR
ISIT_EI_IDE_BIT_SR_RO_ERR:	EQU	0	; SR

ISIT_EI_IDE_MSK_DCR_WO_SRST:	EQU	1<<ISIT_EI_IDE_BIT_DCR_WO_SRST	; DCR
ISIT_EI_IDE_MSK_DCR_WO_nIEN:	EQU	1<<ISIT_EI_IDE_BIT_DCR_WO_nIEN	; DCR
ISIT_EI_IDE_MSK_DHR_RW_DEV:	EQU	1<<ISIT_EI_IDE_BIT_DHR_RW_DEV	; DHR
ISIT_EI_IDE_MSK_ER_RO_ABRT:	EQU	1<<ISIT_EI_IDE_BIT_ER_RO_ABRT	; ER
ISIT_EI_IDE_MSK_SR_RO_BSY:	EQU	1<<ISIT_EI_IDE_BIT_SR_RO_BSY	; SR
ISIT_EI_IDE_MSK_SR_RO_DRDY:	EQU	1<<ISIT_EI_IDE_BIT_SR_RO_DRDY	; SR
ISIT_EI_IDE_MSK_SR_RO_DRQ:	EQU	1<<ISIT_EI_IDE_BIT_SR_RO_DRQ	; SR
ISIT_EI_IDE_MSK_SR_RO_ERR:	EQU	1<<ISIT_EI_IDE_BIT_SR_RO_ERR	; SR

	ENDC ; ---------------------------------------'

	; SCSI
	
	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_EI_SCSI_PORT_DR:	EQU	0
ISIT_EI_SCSI_PORT_RS:	EQU	1
	
ISIT_EI_SCSI_FDC_CTRL_BASE:	EQU	$88
	
ISIT_EI_SCSI_REG_RO_CSD:	EQU	0|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_WO_ODR:	EQU	0|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RW_ICR:	EQU	1|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RW_MR2:	EQU	2|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RW_TCR:	EQU	3|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RO_CSB:	EQU	4|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_WO_SER:	EQU	4|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RO_BSR:	EQU	5|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_WO_SDS:	EQU	5|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RO_IDR:	EQU	6|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_WO_SDT:	EQU	6|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_RO_RPI:	EQU	7|ISIT_EI_SCSI_FDC_CTRL_BASE
ISIT_EI_SCSI_REG_WO_SDI:	EQU	7|ISIT_EI_SCSI_FDC_CTRL_BASE

ISIT_EI_SCSI_BIT_ICR_RW_DBUS:	EQU	0	; 
ISIT_EI_SCSI_BIT_ICR_RW_xATN:	EQU	1	; 
ISIT_EI_SCSI_BIT_ICR_RW_xSEL:	EQU	2	; 
ISIT_EI_SCSI_BIT_ICR_RW_xBSY:	EQU	3	; 
ISIT_EI_SCSI_BIT_ICR_RW_xACK:	EQU	4	; 
ISIT_EI_SCSI_BIT_ICR_WO_DIFF:	EQU	5	; 
ISIT_EI_SCSI_BIT_ICR_RO_LA:	EQU	5	; 
ISIT_EI_SCSI_BIT_ICR_WO_TEST:	EQU	6	; 
ISIT_EI_SCSI_BIT_ICR_RO_AIP:	EQU	6	; 
ISIT_EI_SCSI_BIT_ICR_RW_xRST:	EQU	7	; 
ISIT_EI_SCSI_BIT_MR2_RW_ARB:	EQU	0	; 
ISIT_EI_SCSI_BIT_MR2_RW_DMA:	EQU	1	; 
ISIT_EI_SCSI_BIT_MR2_RW_BSY:	EQU	2	; 
ISIT_EI_SCSI_BIT_MR2_RW_EOP:	EQU	3	; 
ISIT_EI_SCSI_BIT_MR2_RW_PINT:	EQU	4	; 
ISIT_EI_SCSI_BIT_MR2_RW_PCHK:	EQU	5	; 
ISIT_EI_SCSI_BIT_MR2_RW_TARG:	EQU	6	; 
ISIT_EI_SCSI_BIT_MR2_RW_BLK:	EQU	7	; 
ISIT_EI_SCSI_BIT_TCR_RW_xIO:	EQU	0	; 
ISIT_EI_SCSI_BIT_TCR_RW_xCD:	EQU	1	; 
ISIT_EI_SCSI_BIT_TCR_RW_xMSG:	EQU	2	; 
ISIT_EI_SCSI_BIT_TCR_RW_xREQ:	EQU	3	; 
ISIT_EI_SCSI_BIT_CSB_RO_xDBP:	EQU	0	; 
ISIT_EI_SCSI_BIT_CSB_RO_xSEL:	EQU	1	; 
ISIT_EI_SCSI_BIT_CSB_RO_xIO:	EQU	2	; 
ISIT_EI_SCSI_BIT_CSB_RO_xCD:	EQU	3	; 
ISIT_EI_SCSI_BIT_CSB_RO_xMSG:	EQU	4	; 
ISIT_EI_SCSI_BIT_CSB_RO_xREQ:	EQU	5	; 
ISIT_EI_SCSI_BIT_CSB_RO_xBSY:	EQU	6	; 
ISIT_EI_SCSI_BIT_CSB_RO_xRST:	EQU	7	; 
ISIT_EI_SCSI_BIT_BSR_RO_xACK:	EQU	0	; 
ISIT_EI_SCSI_BIT_BSR_RO_xATN:	EQU	1	; 
ISIT_EI_SCSI_BIT_BSR_RO_BSY:	EQU	2	; 
ISIT_EI_SCSI_BIT_BSR_RO_PHSM:	EQU	3	; 
ISIT_EI_SCSI_BIT_BSR_RO_INT:	EQU	4	; 
ISIT_EI_SCSI_BIT_BSR_RO_SPER:	EQU	5	; 
ISIT_EI_SCSI_BIT_BSR_RO_DRQ:	EQU	6	; 
ISIT_EI_SCSI_BIT_BSR_RO_EDMA:	EQU	7	; 

ISIT_EI_SCSI_MSK_ICR_RW_DBUS:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_DBUS	; 
ISIT_EI_SCSI_MSK_ICR_RW_xATN:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_xATN	; 
ISIT_EI_SCSI_MSK_ICR_RW_xSEL:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_xSEL	; 
ISIT_EI_SCSI_MSK_ICR_RW_xBSY:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_xBSY	; 
ISIT_EI_SCSI_MSK_ICR_RW_xACK:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_xACK	; 
ISIT_EI_SCSI_MSK_ICR_WO_DIFF:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_WO_DIFF	; 
ISIT_EI_SCSI_MSK_ICR_RO_LA:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RO_LA	; 
ISIT_EI_SCSI_MSK_ICR_WO_TEST:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_WO_TEST	; 
ISIT_EI_SCSI_MSK_ICR_RO_AIP:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RO_AIP	; 
ISIT_EI_SCSI_MSK_ICR_RW_xRST:	EQU	1<<ISIT_EI_SCSI_BIT_ICR_RW_xRST	; 
ISIT_EI_SCSI_MSK_MR2_RW_ARB:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_ARB	; 
ISIT_EI_SCSI_MSK_MR2_RW_DMA:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_DMA	; 
ISIT_EI_SCSI_MSK_MR2_RW_BSY:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_BSY	; 
ISIT_EI_SCSI_MSK_MR2_RW_EOP:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_EOP	; 
ISIT_EI_SCSI_MSK_MR2_RW_PINT:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_PINT	; 
ISIT_EI_SCSI_MSK_MR2_RW_PCHK:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_PCHK	; 
ISIT_EI_SCSI_MSK_MR2_RW_TARG:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_TARG	; 
ISIT_EI_SCSI_MSK_MR2_RW_BLK:	EQU	1<<ISIT_EI_SCSI_BIT_MR2_RW_BLK	; 
ISIT_EI_SCSI_MSK_TCR_RW_xIO:	EQU	1<<ISIT_EI_SCSI_BIT_TCR_RW_xIO	; 
ISIT_EI_SCSI_MSK_TCR_RW_xCD:	EQU	1<<ISIT_EI_SCSI_BIT_TCR_RW_xCD	; 
ISIT_EI_SCSI_MSK_TCR_RW_xMSG:	EQU	1<<ISIT_EI_SCSI_BIT_TCR_RW_xMSG	; 
ISIT_EI_SCSI_MSK_TCR_RW_xREQ:	EQU	1<<ISIT_EI_SCSI_BIT_TCR_RW_xREQ	; 
ISIT_EI_SCSI_MSK_CSB_RO_xDBP:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xDBP	; 
ISIT_EI_SCSI_MSK_CSB_RO_xSEL:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xSEL	; 
ISIT_EI_SCSI_MSK_CSB_RO_xIO:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xIO	; 
ISIT_EI_SCSI_MSK_CSB_RO_xCD:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xCD	; 
ISIT_EI_SCSI_MSK_CSB_RO_xMSG:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xMSG	; 
ISIT_EI_SCSI_MSK_CSB_RO_xREQ:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xREQ	; 
ISIT_EI_SCSI_MSK_CSB_RO_xBSY:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xBSY	; 
ISIT_EI_SCSI_MSK_CSB_RO_xRST:	EQU	1<<ISIT_EI_SCSI_BIT_CSB_RO_xRST	; 
ISIT_EI_SCSI_MSK_BSR_RO_xACK:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_xACK	; 
ISIT_EI_SCSI_MSK_BSR_RO_xATN:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_xATN	; 
ISIT_EI_SCSI_MSK_BSR_RO_BSY:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_BSY	; 
ISIT_EI_SCSI_MSK_BSR_RO_PHSM:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_PHSM	; 
ISIT_EI_SCSI_MSK_BSR_RO_INT:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_INT	; 
ISIT_EI_SCSI_MSK_BSR_RO_SPER:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_SPER	; 
ISIT_EI_SCSI_MSK_BSR_RO_DRQ:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_DRQ	; 
ISIT_EI_SCSI_MSK_BSR_RO_EDMA:	EQU	1<<ISIT_EI_SCSI_BIT_BSR_RO_EDMA	; 

	ENDC ; ---------------------------------------'

	; IDE IDENTIFY
	
	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_EI_IDE_IDENTIFY_DEV_TYPE:	EQU	0	;
ISIT_EI_IDE_IDENTIFY_REMOVABLE:	EQU	0	;
ISIT_EI_IDE_IDENTIFY_SERIAL:	EQU	20	;
ISIT_EI_IDE_IDENTIFY_FIRMWARE:	EQU	46	;
ISIT_EI_IDE_IDENTIFY_MODEL:	EQU	54	;

	ENDC ; ---------------------------------------'

	; SCSI INQUIRY
	
	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_EI_SCSI_INQUIRY_DEV_TYPE:	EQU	0	;
ISIT_EI_SCSI_INQUIRY_REMOVABLE:	EQU	1	;
ISIT_EI_SCSI_INQUIRY_VENDOR:	EQU	8	;
ISIT_EI_SCSI_INQUIRY_MODEL:	EQU	16	;
ISIT_EI_SCSI_INQUIRY_REVISION:	EQU	32	;
ISIT_EI_SCSI_INQUIRY_ROM:	EQU	45	;

	ENDC ; ---------------------------------------'

; --- EQUATE SYSTEM (ES) ------------------------------------------------------

; /////////////////////////////////////////////////////////////////////////////	
	SECTION	TEXT
; /////////////////////////////////////////////////////////////////////////////	

;	BRA	ISIT_TUI_START
	
;	dc.b	"ProgHEAD"	; ProgHEADER
;	dc.w	ISIT_ED_ASSEMBLY_VERSION	; ProgVERSION
;	dc.w	ISIT_ED_ASSEMBLY_DATE	; ProgDATE
;	dc.b	"IDE/SCSI ID TEST",0	; ProgNAME
;	dc.b	"Kochise",0	; ProgAUTHOR
	
; --- TEXT USER INIT (TUI) ----------------------------------------------------
	
ISIT_TUI_START:	lea	ISIT_DDA_TITLE,A5
	lea	ISIT_BDB_REPORT_BUFFER,A6
	lea	ISIT_DDA_TITLE_COUNTERS,A2
	move.w	#10,D4

ISIT_TUI_REPORT_BUFFER_INIT:	clr.w	D0
	move.b	(A2)+,D0

	BSR	ISIT_TUS_JUSTIFIED_COPY

ISIT_TUI_REPORT_BUFFER_TEST:	move.b	(A5),D0
	BNE	ISIT_TUI_REPORT_BUFFER_NEXT
	addq.l	#1,A5
	BRA	ISIT_TUI_REPORT_BUFFER_TEST
ISIT_TUI_REPORT_BUFFER_NEXT:	DBF	D4,ISIT_TUI_REPORT_BUFFER_INIT
	
; --- TEXT USER MAIN (TUM) ----------------------------------------------------

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

	move.w	#0,ISIT_BFB_BUS_TYPE
	move.w	#ISIT_ED_IDE_NUMPORT-1,D4

ISIT_TUM_IDE_LOOP:	move.w	#ISIT_ED_IDE_NUMPORT-1,D3
	sub.w	D4,D3
	move.w	D3,ISIT_BFB_DEV_NUMB
	move.w	#0,ISIT_BFB_DEV_STATUS
	
	pea	ISIT_TSM_IDE_IDENTIFY_START
	move.w	#$26,-(SP)	; - SUPEXEC -
	TRAP	#14	; *** XBIOS ***
	addq.l	#6,SP

	BSR	ISIT_TUS_PRINT_DEVICE_INFOS

	tst.w	D0
	BNE	ISIT_TUM_IDE_NEXT
		
	BSR	ISIT_TUS_IDE_PRINT_IDENTIFY

ISIT_TUM_IDE_NEXT:	lea	ISIT_DDA_NEXT_LINE,A5
	move.w	#2,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY

	DBF	D4,ISIT_TUM_IDE_LOOP

	ENDC ; ---------------------------------------'

	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

	move.w	#1,ISIT_BFB_BUS_TYPE
	move.w	#ISIT_ED_SCSI_NUMPORT-1,D4

ISIT_TUM_SCSI_LOOP:	move.w	#ISIT_ED_SCSI_NUMPORT-1,D3
	sub.w	D4,D3
	move.w	D3,ISIT_BFB_DEV_NUMB
	move.w	#1,ISIT_BFB_DEV_STATUS

	pea	ISIT_TSM_SCSI_INQUIRY_START
	move.w	#$26,-(SP)	; - SUPEXEC -
	TRAP	#14	; *** XBIOS ***
	addq.l	#6,SP

	BSR	ISIT_TUS_PRINT_DEVICE_INFOS

	tst.w	D0
	BNE	ISIT_TUM_SCSI_NEXT
		
	BSR	ISIT_TUS_SCSI_PRINT_INQUIRY

ISIT_TUM_SCSI_NEXT:	lea	ISIT_DDA_NEXT_LINE,A5
	move.w	#2,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY

	DBF	D4,ISIT_TUM_SCSI_LOOP
	
	ENDC ; ---------------------------------------'
	
	move.b	#0,(A6)+
	
	pea	ISIT_BDB_REPORT_BUFFER
	move.w	#9,-(SP)	; - C_CONWS -
	TRAP	#1	; *** GEMDOS ***
	addq.l	#6,SP
	
; --- TEXT USER EXIT (TUE) ----------------------------------------------------

	clr.w	-(SP)
	pea	ISIT_DDA_REPORT_NAME
	move.w	#$3C,-(SP)	; - F_CREATE -
	TRAP	#1	; *** GEMDOS ***
	addq.l	#8,SP
	
	tst.l	D0
	BMI	ISIT_TUE_EXIT
	
	move.w	D0,D7
	
	pea	ISIT_BDB_REPORT_BUFFER
	move.l	ISIT_BDB_REPORT_LENGHT,-(SP)
	move.w	D0,-(SP)
	move.w	#$40,-(SP)	; - F_WRITE -
	TRAP	#1	; *** GEMDOS ***
	lea	(12,SP),SP
	
	move.w	D7,-(SP)
	move.w	#$3E,-(SP)	; - F_CLOSE -
	TRAP	#1	; *** GEMDOS ***
	addq.l	#4,SP
	
ISIT_TUE_EXIT:	clr.w	-(SP)	; - P_TERM0 -
	TRAP	#1	; *** GEMDOS ***

; --- TEXT USER SUB (TUS) -----------------------------------------------------

ISIT_TUS_PRINT_DEVICE_INFOS:	move.w	ISIT_BFB_BUS_TYPE,D0
	lea	ISIT_DDA_BUS,A5
	lea	(A5,D0.w*4),A5
	move.w	#4,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+
	
	move.w	ISIT_BFB_DEV_NUMB,D0
	addi.b	#'0',D0
	move.b	D0,(A6)+
	move.w	#'  ',(A6)+
	
	move.w	ISIT_BFB_DEV_STATUS,D1
	lea	ISIT_DAT_PROTOCOL,A5
	movea.l	(A5,D1.w*4),A5
	
	cmpi.w	#2,D1
	BGE	ISIT_TUS_DEVICE_ERROR

	move.w	#0,D2
	move.w	#9,D0
	
	BRA	ISIT_TUS_PRINT_COMM_TYPE

ISIT_TUS_DEVICE_ERROR:	move.w	#1,D2
	move.w	#24,D0
	
ISIT_TUS_PRINT_COMM_TYPE:	BSR	ISIT_TUS_JUSTIFIED_COPY

	move.w	D2,D0

	add.l	#3,ISIT_BDB_REPORT_LENGHT

	RTS

; ----------------------

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_TUS_IDE_PRINT_IDENTIFY:	clr.l	D6

	move.w	ISIT_BFB_DEV_NUMB,D0
	lea	ISIT_DAT_IDE_ID_BUFFERS,A4
	movea.l	(A4,D0.w*4),A4

	lea	ISIT_DDA_SCSI_DEV_TYPE,A5

	move.w	ISIT_BFB_DEV_STATUS,D0
	cmpi.w	#1,D0
	BNE	ISIT_TUS_IDE_COPY_DEV_TYPE
	
	move.w	(ISIT_EI_IDE_IDENTIFY_DEV_TYPE,A4),D0
	asr.w	#8,D0
	andi.w	#$001F,D0
	lea	(A5,D0.w*8),A5
		
ISIT_TUS_IDE_COPY_DEV_TYPE:	move.w	#8,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+
	
	lea	ISIT_DDA_REMOVABLE_CAPABILITY,A5
	move.w	(ISIT_EI_IDE_IDENTIFY_REMOVABLE,A4),D0
	asr.w	#7,D0
	andi.w	#$0001,D0
	lea	(A5,D0.w*4),A5
	move.w	#10,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	
	lea	ISIT_DDA_EMPTY,A5
	move.w	#9,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY

	lea	(ISIT_EI_IDE_IDENTIFY_MODEL,A4),A5
	move.w	#40,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+

	lea	(ISIT_EI_IDE_IDENTIFY_SERIAL,A4),A5
	move.w	#20,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+

	lea	(ISIT_EI_IDE_IDENTIFY_FIRMWARE,A4),A5
	move.w	#8,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	
	add.l	#3,ISIT_BDB_REPORT_LENGHT
	
	RTS

	ENDC ; ---------------------------------------'

; ----------------------

	; SCSI

	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_TUS_SCSI_PRINT_INQUIRY:	move.w	ISIT_BFB_DEV_NUMB,D0
	lea	ISIT_DAT_SCSI_ID_BUFFERS,A4
	movea.l	(A4,D0.w*4),A4

	lea	ISIT_DDA_SCSI_DEV_TYPE,A5

	move.b	(ISIT_EI_SCSI_INQUIRY_DEV_TYPE,A4),D0
	andi.w	#$001F,D0
	lea	(A5,D0.w*8),A5
	move.w	#8,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+
	
	lea	ISIT_DDA_REMOVABLE_CAPABILITY,A5
	move.b	(ISIT_EI_SCSI_INQUIRY_REMOVABLE,A4),D0
	asr.w	#7,D0
	andi.w	#$0001,D0
	lea	(A5,D0.w*4),A5
	move.w	#10,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	
	lea	(ISIT_EI_SCSI_INQUIRY_VENDOR,A4),A5
	move.w	#8,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+

	lea	(ISIT_EI_SCSI_INQUIRY_MODEL,A4),A5
	move.w	#40,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+

	lea	(ISIT_EI_SCSI_INQUIRY_SERIAL,A4),A5
	move.w	#20,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY
	move.b	#' ',(A6)+

	lea	ISIT_DDA_EMPTY,A5
	move.w	#8,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY

	lea	ISIT_DDA_NEXT_LINE,A5
	move.w	#2,D0
	BSR	ISIT_TUS_JUSTIFIED_COPY

	add.l	#5,ISIT_BDB_REPORT_LENGHT

	RTS

	ENDC ; ---------------------------------------'

; ----------------------

	; A5 : Source
	; A6 : Destination
	; D0 : Counter
ISIT_TUS_JUSTIFIED_COPY:	move.l	ISIT_BDB_REPORT_LENGHT,D7
	subq.l	#1,D0
	BMI	ISIT_TUS_JUSTIFIED_COPY_END

ISIT_TUS_RAW_COPY_LOOP:	move.b	(A5)+,D1
	BEQ	ISIT_TUS_FILL_COPY_LOOP
	move.b	D1,(A6)+
	addq.l	#1,D7

	DBF	D0,ISIT_TUS_RAW_COPY_LOOP
		
	BRA	ISIT_TUS_JUSTIFIED_COPY_END

ISIT_TUS_FILL_COPY_LOOP:	move.b	#' ',(A6)+
	addq.l	#1,D7

	DBF	D0,ISIT_TUS_FILL_COPY_LOOP

ISIT_TUS_JUSTIFIED_COPY_END:	move.l	D7,ISIT_BDB_REPORT_LENGHT
	
	RTS

; =============================================================================

; --- TEXT SUPER MAIN (TSM) ---------------------------------------------------

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_TSM_IDE_IDENTIFY_START:	lea	ISIT_DAT_IDE_REG_IO,A6
	move.b	#ISIT_EI_IDE_MSK_DCR_WO_nIEN,([ISIT_EI_IDE_REG_RW_DHR,A6])

	move.w	ISIT_BFB_DEV_NUMB,D0
	asl.w	#ISIT_EI_IDE_BIT_DHR_RW_DEV,D0
	move.b	D0,([ISIT_EI_IDE_REG_RW_DHR,A6])

ISIT_TSM_IDE_CHECK_STATUS:	move.b	([ISIT_EI_IDE_REG_RO_SR,A6]),D0

	; WARNING : Tricking device selection to check if there's any device really connected !
	move.b	D0,D1
	andi.b	#$F0,D1
	cmpi.b	#$F0,D1
	BEQ	ISIT_TSM_IDE_NO_DEVICE

	andi.b	#(ISIT_EI_IDE_MSK_SR_RO_BSY|ISIT_EI_IDE_MSK_SR_RO_DRQ),D0
	BNE	ISIT_TSM_IDE_CHECK_STATUS

	move.w	ISIT_BFB_DEV_STATUS,D0
	BNE	ISIT_TSM_IDE_PACKET_CMD
	
	move.b	#ISIT_ED_IDE_CMD_ID_DEV,D0	
	BRA	ISIT_TSM_IDE_WRITE_CMD
	
ISIT_TSM_IDE_PACKET_CMD:	move.b	#ISIT_ED_IDE_CMD_ID_PDEV,D0
ISIT_TSM_IDE_WRITE_CMD:	move.b	D0,([ISIT_EI_IDE_REG_WO_CR,A6])

ISIT_TSM_IDE_SET_TIMEOUT:	lea	$4BA.w,A5
	move.l	(A5),D5
	add.l	#200*ISIT_ED_IDE_CLK_WAIT,D5
	SCS	ISIT_BFB_TIMEOUT_OVERLAP
	
ISIT_TSM_IDE_CHECK_PIO_STATE:	move.b	([ISIT_EI_IDE_REG_RO_SR,A6]),D0

	btst.l	#ISIT_EI_IDE_BIT_SR_RO_ERR,D0
	BNE	ISIT_TSM_IDE_CHECK_PACKET_DEV

	andi.b	#(ISIT_EI_IDE_MSK_SR_RO_BSY|ISIT_EI_IDE_MSK_SR_RO_DRQ),D0
	BEQ	ISIT_TSM_IDE_DEV_NOT_RESP

	cmpi.b	#ISIT_EI_IDE_MSK_SR_RO_DRQ,D0
	BNE	ISIT_TSM_IDE_CHECK_PIO_STATE

	lea	ISIT_DAT_IDE_ID_BUFFERS,A0
	move.w	ISIT_BFB_DEV_NUMB,D0
	movea.l	(A0,D0.w*4),A0
	move.w	#256-1,D0

ISIT_TSM_IDE_READ_PIO_DATA:	move.w	([ISIT_EI_IDE_REG_RW_DR,A6]),(A0)+
	DBF	D0,ISIT_TSM_IDE_READ_PIO_DATA

ISIT_TSM_IDE_IDENTIFY_END:	RTS

	; ----------------------

ISIT_TSM_IDE_DEV_NOT_RESP:	tst.w	ISIT_BFB_TIMEOUT_OVERLAP
	BEQ	ISIT_TSM_IDE_WAIT_TIMEOUT

	tst.l	(A5)
	BMI	ISIT_TSM_IDE_CHECK_PIO_STATE

ISIT_TSM_IDE_WAIT_TIMEOUT:	cmp.l	(A5),D5
	BGT	ISIT_TSM_IDE_CHECK_PIO_STATE

	move.w	#2,ISIT_BFB_DEV_STATUS

	RTS

ISIT_TSM_IDE_NO_DEVICE:	move.w	#3,ISIT_BFB_DEV_STATUS

	RTS

	; ----------------------

ISIT_TSM_IDE_CHECK_PACKET_DEV:	btst.b	#ISIT_EI_IDE_BIT_ER_RO_ABRT,([ISIT_EI_IDE_REG_RO_ER,A6])
	BEQ	ISIT_TSM_IDE_DEV_NOT_RESP

	cmpi.b	#$14,([ISIT_EI_IDE_REG_RW_CLR,A6])
	BNE	ISIT_TSM_IDE_DEV_NOT_RESP

	cmpi.b	#$EB,([ISIT_EI_IDE_REG_RW_CHR,A6])
	BNE	ISIT_TSM_IDE_DEV_NOT_RESP

	tst.w	ISIT_BFB_DEV_STATUS
	BNE	ISIT_TSM_IDE_DEV_NOT_RESP

	move.w	#1,ISIT_BFB_DEV_STATUS
	
	BRA	ISIT_TSM_IDE_CHECK_STATUS

	ENDC ; ---------------------------------------'

; ----------------------

	; SCSI

	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_TSM_SCSI_INQUIRY_START:

ISIT_TSM_SCSI_INQUIRY_END:	RTS

	ENDC ; ---------------------------------------'

; --- TEXT SUPER SUB (TSS) ----------------------------------------------------

; /////////////////////////////////////////////////////////////////////////////
	SECTION	DATA
; /////////////////////////////////////////////////////////////////////////////

; --- DATA ADDRESS TABLE (DAT) ------------------------------------------------

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_DAT_IDE_REG_IO:	dc.l	$00F00000	; DP/DR
	dc.l	$00F00005	; ER/FR
	dc.l	$00F00009	; SCR
	dc.l	$00F0000D	; SNR/LBA0700
	dc.l	$00F00011	; CLR/LBA1508
	dc.l	$00F00015	; CHR/LBA2316
	dc.l	$00F00019	; DHR/LBA2724
	dc.l	$00F0001D	; CR/SR
	dc.l	$00F00039	; ASR/DCR

	ENDC ; ---------------------------------------'
	
	; SCSI
	
	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_DAT_SCSI_REG_IO:	dc.l	$FFFF8604	; DA
	dc.l	$FFFF8606	; RS	

	ENDC ; ---------------------------------------'
	
	; IDE
	
	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_DAT_IDE_ID_BUFFERS:	dc.l	ISIT_BDB_IDE0_ID
	dc.l	ISIT_BDB_IDE1_ID

	ENDC ; ---------------------------------------'

	; SCSI

	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_DAT_SCSI_ID_BUFFERS:	dc.l	ISIT_BDB_SCSI0_ID
	dc.l	ISIT_BDB_SCSI1_ID
	dc.l	ISIT_BDB_SCSI2_ID
	dc.l	ISIT_BDB_SCSI3_ID
	dc.l	ISIT_BDB_SCSI4_ID
	dc.l	ISIT_BDB_SCSI5_ID
	dc.l	ISIT_BDB_SCSI6_ID
	dc.l	ISIT_BDB_SCSI7_ID

	ENDC ; ---------------------------------------'

ISIT_DAT_PROTOCOL:	dc.l	ISIT_DDA_PROTOCOL_COMMAND
	dc.l	ISIT_DDA_PROTOCOL_PACKET
	dc.l	ISIT_DDA_PROTOCOL_DEV_NOT_RESP
	dc.l	ISIT_DDA_PROTOCOL_NO_DEV_CON

; --- DATA DEFINED ARRAY (DDA) ------------------------------------------------

ISIT_DDA_TITLE:	dc.b	"/// IDE/SCSI IDENTIFY TEST - KOSS 2001-2002 ///",13,10
	dc.b	13,10
	dc.b	"BUS",0
	dc.b	"DEV",0
	dc.b	"PROTOCOL",0
	dc.b	"TYPE",0
	dc.b	"REMOVABLE",0
	dc.b	"VENDOR",0
	dc.b	"MODEL",0
	dc.b	"SERIAL",0
	dc.b	"FIRMWARE"
	dc.b	13,10
ISIT_DDA_NEXT_LINE:	dc.b	13,10
	dc.b	1

	EVEN

ISIT_DDA_TITLE_COUNTERS:	dc.b	51,4,4,9,9,10,9,41,21,8,4

	EVEN

ISIT_DDA_BUS:	dc.b	" IDE"
	dc.b	"SCSI"
	
	EVEN

ISIT_DDA_PROTOCOL_COMMAND:	dc.b	"COMMAND",0
	EVEN
ISIT_DDA_PROTOCOL_PACKET:	dc.b	"PACKET",0
	EVEN
ISIT_DDA_PROTOCOL_DEV_NOT_RESP:	dc.b	"Device Not Responding...",0
	EVEN
ISIT_DDA_PROTOCOL_NO_DEV_CON:	dc.b	"No Device Connected...",0

	EVEN

ISIT_DDA_SCSI_DEV_TYPE:	dc.b	"DIRECT  "
	dc.b	"SEQUENT "
	dc.b	"PRINTER "
	dc.b	"PROCESS "
	dc.b	"WORM    "
	dc.b	"CD-ROM  "
	dc.b	"SCANNER "
	dc.b	"OPTICAL "
	dc.b	"JUKEBOX "
	dc.b	"COMM    "
	dc.b	"ACS IT8 "
	dc.b	"ACS IT8 "
	dc.b	"ARRAY   "
	dc.b	"ENCLOS  "
	dc.b	"RED BLK "
	dc.b	"OPT CARD"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"Reserved"
	dc.b	"UNKNOWN "

	EVEN
	
ISIT_DDA_REMOVABLE_CAPABILITY:	dc.b	"NO ",0
	dc.b	"YES",0
	
	EVEN
	
ISIT_DDA_REPORT_NAME:	dc.b	"REPORT.LOG",0

	EVEN
	
	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_DDA_SCSI_INQUIRY_MESSAGE:	dc.b	$12	; INQUIRY
	dc.b	0	; LUN
	dc.b	0	; Page code
	dc.b	0	; Reserved
	dc.b	96	; INQUIRY page
	dc.b	0	; Control

	EVEN
	
	ENDC ; ---------------------------------------'
	
ISIT_DDA_EMPTY:	dc.b	0

	EVEN

; /////////////////////////////////////////////////////////////////////////////	
	SECTION	BSS
; /////////////////////////////////////////////////////////////////////////////	

; --- BSS DATA BUFFER (BDB) ---------------------------------------------------

	; IDE

	IFNE ISIT_EA_IDE_ACTIVE ; --------------------,

ISIT_BDB_IDE0_ID:	ds.w	256
ISIT_BDB_IDE1_ID:	ds.w	256

	ENDC ; ---------------------------------------'

	; SCSI

	IFNE ISIT_EA_SCSI_ACTIVE ; -------------------,

ISIT_BDB_SCSI0_ID:	ds.b	96
ISIT_BDB_SCSI1_ID:	ds.b	96
ISIT_BDB_SCSI2_ID:	ds.b	96
ISIT_BDB_SCSI3_ID:	ds.b	96
ISIT_BDB_SCSI4_ID:	ds.b	96
ISIT_BDB_SCSI5_ID:	ds.b	96
ISIT_BDB_SCSI6_ID:	ds.b	96
ISIT_BDB_SCSI7_ID:	ds.b	96

	ENDC ; ---------------------------------------'

ISIT_BDB_REPORT_BUFFER:	ds.b	2048
ISIT_BDB_REPORT_LENGHT:	ds.l	1

; --- BSS FLAG BUFFER (BFB) ---------------------------------------------------

ISIT_BFB_DEV_NUMB:	ds.w	1
ISIT_BFB_BUS_TYPE:	ds.w	1
ISIT_BFB_DEV_STATUS:	ds.w	1

ISIT_BFB_TIMEOUT_OVERLAP:	ds.w	1